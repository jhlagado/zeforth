
;   0<          ( n -- t )
;               Return true if n is negative.
mCODE	2,'0<', ZLESS
	SLA B	; sign bit -> cy flag
	SBC	A,A	; propagate cy through A
	LD	B,A	; put 0000 or FFFF in TOS
	LD	C,A
	mNEXT

;   AND         ( w w -- w )
;               Bitwise AND.
mCODE	3, 'AND', ANDD
	POP	HL
	LD	A,B
	AND	H
	LD	B,A
	LD	A,C
	AND	L
	LD	C,A
	mNEXT

;   OR          ( w w -- w )
;               Bitwise inclusive OR.
mCODE	2,'OR', ORR
	POP	HL
	LD	A,B
	OR	H
	LD	B,A
	LD	A,C
	OR	L
	LD	C,A
	mNEXT

;   XOR         ( w w -- w )
;               Bitwise exclusive
mCODE	3, 'XOR', XORR
	POP	HL
	LD	A,B
	XOR	H
	LD	B,A
	LD	A,C
	XOR	L
	LD	C,A
	mNEXT

;   +           ( w w -- sum )
;               Add top two items.
mCOLON	1,'+', PLUS
    DW	UMPlus, DROP, EXIT

;   NOT         ( w -- w )
;               One's complement of tos.
mCOLON  3, 'NOT', INVER
    DW  DOLIT, -1, XORR, EXIT

;   NEGAT      ( n -- -n )
;               Two's complement of tos.
mCOLON	6,'NEGATE',NEGAT
    DW	INVER, DOLIT, 1, Plus, EXIT

;   DNEGATE     ( d -- -d )
;               Two's complement of top double.
mCOLON	7,'DNEGATE',DNEGA
	DW	INVER, ToR, INVER
	DW	DOLIT, 1, UMPlus
	DW	RFrom, Plus, EXIT

;   -           ( n1 n2 -- n1-n2 )
;               Subtraction.
mCOLON	1, '-', SUBB
	DW	NEGAT,Plus,EXIT

;   ABS         ( n -- n )
;               Return the absolute value of n.
mCOLON	3,'ABS',ABSS
    DW	DUPP, ZLESS, QBRAN, ABS1
    DW	NEGAT
ABS1:		
    DW	EXIT

;   =           ( w w -- t )
;               Return true if top two are equal.
mCOLON	1, '=', EQUAL
    DW      XORR
    DW      QBRAN,EQU1
    DW      DOLIT,0,EXIT            ;false flag
EQU1:           
    DW      DOLIT,-1,EXIT           ;true flag

;   U<          ( u u -- t )
;               Unsigned compare of top two items.
mCOLON	2,'U<',ULESS
    DW      DDUP,XORR,ZLESS
    DW      QBRAN,ULES1
    DW      SWAP,DROP,ZLESS,EXIT
ULES1:          
    DW      SUBB,ZLESS,EXIT

;   <           ( n1 n2 -- t )
;               is less than
mCOLON	1,'<',LESS
	DW	DDUP, XORR, ZLESS, QBRAN, LESS1
	DW	DROP, ZLESS, EXIT
LESS1:		
    DW	SUBB, ZLESS, EXIT

;   MAX         ( n n -- n )
;               Return the greater of two top stack items.
mCOLON	3,'MAX',MAX
	DW	DDUP, LESS, QBRAN, MAX1
    DW	SWAP
MAX1:		
    DW	DROP,EXIT

;   MIN         ( n n -- n )
;               Return the smaller of top two stack items.
mCOLON	3,'MIN',MIN
    DW      DDUP,SWAP,LESS
    DW      QBRAN,MIN1
    DW      SWAP
MIN1:           
    DW      DROP,EXIT

;   WITHIN      ( u ul uh -- t )
;               Return true if u is within the range of ul and uh.
mCOLON	6,'WITHIN',WITHI
	DW	OVER,SUBB,ToR			;ul <= u < uh
	DW	SUBB,RFrom,ULess,EXIT

;   UM+         ( w w -- w cy )
;               Add two numbers, return the sum and carry flag.
hCODE	3,'um+',UMPlus
    POP	HL	            ;10t
	ADD	HL,BC	        ;11t
	PUSH	HL	        ;11t
	LD	BC,0	        ;10t
	JR	NC,UMP1         ;12/7t
UMP2:		
    INC	BC	            ; 6t
UMP1:		
    mNEXT

; UM/MOD      ( udl udh u -- ur uq )
; Unsigned divide of a double by a single. Return mod and quotient.
; Note: Less range than eForth because most significant word discarded TODO: fix 
mCOLON  6,'UM/MOD',UMMOD
    DW  SWAP,DROP, UMSLMOD2, EXIT

; UM/MOD2	( ud u1 -- u2 u3 )		
; Unsigned division of a double-cell number ud by a single-cell
; number u1. Return remainder u2 and quotient u3.
; Note: Less range than eForth TODO: fix 
mCODE	6,'UM/MOD2',UMSLMOD2
    LD	A,B
	OR	C
	JR	Z,UMMOD2	;?zero divisor
UMMOD11: 	
    PUSH	BC
	EXX
	POP	BC
	POP	HL
	POP	DE
	LD	A,L
	SUB	C
	LD	A,H
	SBC	A,B
	JR	NC,UMMOD1	;?too big
UMMOD12: 	
    LD	A,H
    LD	H,L
	LD	L,D
	LD	D,8
	PUSH	DE
	CALL	UMMOD3
	POP	DE
	PUSH	HL
	LD	L,E
	CALL	UMMOD3
	LD	D,A
	LD	E,H
	POP	BC
	LD	H,C
	PUSH	DE
	PUSH	HL
	EXX
	POP	BC
	mNEXT
UMMOD2:		
    LD	BC,ERR_DIVZERO ; divide by 0
	JP	THROWX
UMMOD1:		
    LD	BC,ERR_OUTOFRANGE ; result out of range
	JP	THROWX
UMMOD4:		
    LD	E,A
	LD	A,H
	SUB	C
	LD	H,A
	LD	A,E
	SBC	A,B
	JR	NC,UMMOD5
UMMOD13: 	
    LD	A,H
	ADD	A,C
	LD	H,A
	LD	A,E
	DEC	D
	RET	Z
UMMOD3:		
    ADD	HL,HL
	RLA
	JR	NC,UMMOD4
UMMOD14: 	
    LD	E,A
	LD	A,H
	SUB	C
	LD	H,A
	LD	A,E
	SBC	A,B
UMMOD5:		
    INC	L
	DEC	D
	JR	NZ,UMMOD3
UMMOD15: 	
    RET

;   FM/MOD	( d n1 -- n2 n3 )		\ CORE
;		Signed floored divide of double by single. Return mod n2
;		and quotient n3.
; TODO: this is usually called M/MOD in eForth
mCOLON	6,'M/MOD',MSMOD
    DW      DUPP,ZLESS,DUPP,TOR
    DW      QBRAN,MMOD1
    DW      NEGAT,TOR,DNEGA,RFROM
MMOD1:          
    DW      TOR,DUPP,ZLESS
    DW      QBRAN,MMOD2
    DW      RAT,PLUS
MMOD2:          
    DW      RFROM,UMMOD,RFROM
    DW      QBRAN,MMOD3
    DW      SWAP,NEGAT,SWAP
MMOD3:          
    DW      EXIT

;   /MOD        ( n n -- r q )
;               Signed divide. Return mod and quotient.
mCOLON	4,'/MOD',SLMOD
    DW  OVER,ZLESS,SWAP,MSMOD,EXIT

;   MOD         ( n n -- r )
;               Signed divide. Return mod only.
mCOLON	3,'MOD',MODD
	DW	SLMOD,DROP,EXIT
	
;   /           ( n n -- q )
;               Signed divide. Return quotient only.
mCOLON	1,'/',SLASH
    DW	SLMOD,SWAP,DROP,EXIT

;; Multiply

;   UM*         ( u u -- ud )
;               Unsigned multiply. Return double product.
mCODE	3,'UM*',UMSTA
	EX	DE,HL
	EX	(SP),HL
	EX	DE,HL
	LD	HL,0
	LD	A,010h
UMSTA1: 	
    ADD	HL,HL
	EX	DE,HL
	ADC	HL,HL
	EX	DE,HL
	JR	NC,UMSTA3
UMSTA2: 	
    ADD	HL,BC
	JR	NC,UMSTA3
UMSTA5: 	
    INC	DE
UMSTA3: 	
    DEC	A
	JR	NZ,UMSTA1
UMSTA4: 	
    LD	B,D
	LD	C,E
	EX	(SP),HL
	mNEXTHL

;   *           ( n n -- n )
;               Signed multiply. Return single product.
mCOLON	1,'*',STAR
	DW	UMSTA, DROP, EXIT

;   M*          ( n n -- d )
;               Signed multiply. Return double product.
mCOLON	2,'M*',MSTAR,
	DW	DDUP,XORR,ZLESS,ToR,ABSS,SWAP,ABSS
	DW	UMSTA,RFrom,QBRAN,MSTAR1
	DW	DNEGA
MSTAR1:		
    DW	EXIT

;   */MOD       ( n1 n2 n3 -- r q )
;               Multiply n1 and n2, then divide by n3. Return mod and quotient.
mCOLON	5,'*/MOD',SSMOD
	DW	ToR,MStar,RFrom,MSMOD,EXIT

;   */          ( n1 n2 n3 -- q )
;               Multiply n1 by n2, then divide by n3. Return quotient only.
mCOLON	2,'*/',STASL
	DW	SSMOD,SWAP,DROP,EXIT

;   CELL+       ( a -- a )
;               Add cell size in byte to address.
mCOLON	5,'CELL+',CELLP
    DW  DOLIT,CELLL,PLUS,EXIT

;   CELL-       ( a -- a )
;               Subtract cell size in byte from address.
mCOLON	5,'CELL-',CELLM
	DW	DOLIT, -CELLL, PLUS, EXIT

;   CELLS       ( n -- n )
;               Multiply tos by cell size in bytes.
mCOLON	5,'CELLS',CELLS
    DW  DOLIT,CELLL,STAR,EXIT
    
    
;;;;;  EXTRAS ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

mCOLON  1,'>',GREAT
    DW  SWAP,LESS,EXIT

mCOLON  2,'1+',ONEP
    DW  DOLIT,1,PLUS,EXIT

mCOLON  2,'1-',ONEM
    DW  DOLIT,1,SUBB,EXIT

mCOLON  2,'2*',TWSTA
    DW  DUPP,PLUS,EXIT

mCODE  2,'2/',TWSLA
    SRL B
    RR C
    mNEXT


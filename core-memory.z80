;   !           ( w a -- )
;               Pop the data stack to memory.
mCODE	1,'!',Store
	LD	H,B
	LD	L,C
	POP	BC
	LD	(HL),C
	INC	HL
	LD	(HL),B
	POP	BC
	mNEXT

;   @           ( a -- w )
;               Push memory location to the data stack.
mCODE	1,'@', AT
	LD	H,B
	LD	L,C
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	mNEXT

;   C!          ( c b -- )
;               Pop the data stack to byte memory.
mCODE	2,'C!',CSTOR
	LD	H,B
	LD	L,C
	POP	BC
	LD	(HL),C
	POP	BC
	mNEXT

;   C@          ( b -- c )
;               Push byte memory location to the data stack.
mCODE	2,'C@',CAT
	LD	A,(BC)
	LD	C,A
	LD	B,0
	mNEXT

;   R@          ( -- w )
;               Copy top of return stack to the data stack.
mCODE	COMPO+2,'R@',RAT
	PUSH	BC
	LD	C,(IX+0)
	LD	B,(IX+1)
	mNEXT

;   RP@         ( -- a )
;               Push the current RP to the data stack
mCODE	COMPO+3, 'rp@', RPAT
    DB	0C5h	; PUSH	BC	;11t
    DB	0DDh,0E5h ; PUSH IX	;15t
    DB	0C1h	; POP	BC	;10t
    mNEXT


;   RP!         ( a -- )
;               Set the return stack pointer.
mCODE	COMPO+3,'rp!',RPSTO
	DB	0C5h	; PUSH	BC	;11t
	DB	0DDh,0E1h ; POP IX	;14t
	DB	0C1h	; POP	BC	;10t
	mNEXT

;   SP@         ( -- a )
;               Push the current data stack pointer.
mCODE	3, 'sp@', SPAT
	DB	0C5h	; PUSH	BC	;11t
	DB	021h	; LD	HL,0	;10t
	DW	0
	DB	039h	; ADD	HL,SP	;11t
	DB	044h	; LD	B,H	; 4t
	DB	04Dh	; LD	C,L	; 4t
	mNEXT

;   SP!         ( a -- )
;               Set the data stack pointer.
mCODE	3,'sp!',SPSTO
	DB	060h	; LD	H,B	; 4t
	DB	069h	; LD	L,C	; 4t
	DB	0F9h	; LD	SP,HL	; 6t
	DB	0C1h	; POP	BC	;10t
	mNEXT


;   +!          ( n a -- )
;               Add n to the contents at address a.
mCOLON  2,'+!',PSTOR
    DW      SWAP,OVER,AT,PLUS
    DW      SWAP,STORE,EXIT

;   2!          ( d a -- )
;               Store the double integer to address a.
mCOLON  2,'2!',DSTOR
    DW      SWAP,OVER,STORE
    DW      CELLP,STORE,EXIT

;   2@          ( a -- d )
;               Fetch double integer from address a.
mCOLON  2,'2@',DAT
    DW      DUPP,CELLP,AT
    DW      SWAP,AT,EXIT

;   COUNT       ( b -- b +n )
;               Return count byte of a string and add 1 to byte address.
mCOLON  5,'COUNT',COUNT
    DW      DUPP,DOLIT,1,PLUS
    DW      SWAP,CAT,EXIT

;   HERE        ( -- a )
;               Return the top of the code dictionary.
mCOLON  4,'HERE',HERE
    DW  CP,AT,EXIT

;   PAD         ( -- a )
;               Return the address of a temporary buffer.
mCOLON  3,'PAD',PAD
    DW  HERE,DOLIT,80,PLUS,EXIT

;   TIB         ( -- a )
;               Return the address of the terminal input buffer.

mCOLON  3,'TIB',TIB
    DW      NTIB,CELLP,AT,EXIT

;   @EXECUTE    ( a -- )
;               Execute vector stored in address a.

hCOLON  8,'@EXECUTE',ATEXE
    DW      AT,QDUP                 ;?address or zero
    DW      QBRAN,EXE1
    DW      EXECU                   ;execute if non-zero
EXE1:           
    DW      EXIT                    ;do nothing if zero

;   CMOVE       ( b1 b2 u -- )
;               Copy u bytes from b1 to b2.

mCOLON  5,'CMOVE',CMOVE
    DW      TOR
    DW      BRAN,CMOV2
CMOV1:          
    DW      TOR,DUPP,CAT
    DW      RAT,CSTOR
    DW      DOLIT,1,PLUS
    DW      RFROM,DOLIT,1,PLUS
CMOV2:          
    DW      DONXT,CMOV1
    DW      DDROP,EXIT

;   FILL        ( b u c -- )
;               Fill u bytes of character c to area beginning at b.

mCOLON  4,'FILL',FILL
    DW      SWAP,TOR,SWAP
    DW      BRAN,FILL2
FILL1:          
    DW      DDUP,CSTOR,DOLIT,1,PLUS
FILL2:          
    DW      DONXT,FILL1
    DW      DDROP,EXIT

;   -TRAILING   ( b u -- b u )
;               Adjust the count to eliminate trailing white space.

mCOLON  9,'-TRAILING',DTRAI
    DW      TOR
    DW      BRAN,DTRA2
DTRA1:          
    DW      BLANK,OVER,RAT,PLUS,CAT,LESS
    DW      QBRAN,DTRA2
    DW      RFROM,DOLIT,1,PLUS,EXIT ;adjusted count
DTRA2:          
    DW      DONXT,DTRA1
    DW      DOLIT,0,EXIT            ;count=0

;   PACK$       ( b u a -- a )
;               Build a counted string with u characters from b. Null fill.

mCOLON  5,'PACK$',PACKS
    DW      ALGND,DUPP,TOR          ;strings only on cell boundary
    DW      OVER,DUPP,DOLIT,0
    DW      DOLIT,CELLL,UMMOD,DROP  ;count mod cell
    DW      SUBB,OVER,PLUS
    DW      DOLIT,0,SWAP,STORE      ;null fill cell
    DW      DDUP,CSTOR,DOLIT,1,PLUS ;save count
    DW      SWAP,CMOVE,RFROM,EXIT   ;move string

;   ALIGNED     ( b -- a )
;               Align address to the cell boundary.
mCOLON  7,'ALIGNED', ALGND
    DW      DUPP,DOLIT,0,DOLIT,CELLL
    DW      UMMOD,DROP,DUPP
    DW      QBRAN,ALGN1
    DW      DoLIT,CELLL,SWAP,SUBB
ALGN1:          
    DW      PLUS,EXIT
